body file control
{
  namespace => "file_permissions";
}
bundle agent file_permissions
# @brief Manage unix file permissions
# @var file_permissions:file_permissions.files - array of dicts having `path`, `mode` and `why` keys, e.g. `[{"path": "/etc/hosts", "mode": "644", "why": "Any user should be able to resolve names from /etc/hosts"}]`
#
# **Notes:**
# - Each file (or directory) must be enumerated specifically (permissions are not recursively applied)
# - If a file does not exist, it will result in a promise not kept
# - This module will have no effect on windows systems (as unix file modes are not supported)
{
  vars:
    !windows::
      # We pull the index from the files variable so that we can iterate over each file to manage it's mode
      "i" slist => getindices( files );

  files:
    !windows::
      "$(files[$(i)][path])"
      perms => default:m( "$(files[$(i)][mode])" ),
      comment => "$(files[$(i)][why])";
}
